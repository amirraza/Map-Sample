apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.mapsample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            useProguard true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            useProguard false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "env"
    productFlavors {
        prod {
            dimension "env"
            manifestPlaceholders = [appLabel: "Map Sample"]
            buildConfigField 'String', "BASE_URL", "\"https://pastebin.com\""
        }
        dev {
            dimension "env"
            versionCode 100 + android.defaultConfig.versionCode
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appLabel: "Map Sample - Dev"]
            buildConfigField 'String', "BASE_URL", "\"https://pastebin.com\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def apkName = "mapsample" +
                    "-v" + output.versionNameOverride +
                    "-c" + android.defaultConfig.versionCode +
                    "-" + releaseTime() +
//                    "-" + variant.productFlavors[0].name +
                    "-" + variant.buildType.name +
                    ".apk"

            outputFileName = new File("../../" + apkName)
        }
    }

    kapt {
        generateStubs = true
    }

    dataBinding {
        enabled = true
    }
}

static def releaseTime() {
    return new Date().format("MMM-dd")
}

//Dependencies of Network Libs
final networkDependencies = [
        retrofit             : "com.squareup.retrofit2:retrofit:$retrofitVersion",
        retrofitGsonConverter: "com.squareup.retrofit2:converter-gson:$retrofitVersion",
        okHttp3Logging       : "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
]

//Dependencies of Android Support Libs
final supportDependencies = [
        cardView        : "androidx.cardview:cardview:$cardviewVersion",
        appCompat       : "androidx.appcompat:appcompat:$appCompatVersion",
        recyclerView    : "androidx.recyclerview:recyclerview:$recyclerViewVersion",
        constraintLayout: "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion",
        coreKtx         : "androidx.core:core-ktx:$coreKtxVersion",
        koin            : "org.koin:koin-android:$koin_version",
        koinVM          : "org.koin:koin-android-viewmodel:$koin_version",
        koinScope       : "org.koin:koin-android-scope:$koin_version",
        preferences     : "androidx.preference:preference:$preferenceVersion"
]

//Dependencies of Android Jetpack Libs
final jetpackDependencies = [
        room     : "androidx.room:room-runtime:$roomVersion",
        lifecycle: "android.arch.lifecycle:extensions:$archLifecycleVersion"
]

//Dependencies of Android Kapt
final kaptDependencies = [
        room     : "androidx.room:room-compiler:$roomVersion",
        lifecycle: "android.arch.lifecycle:compiler:$archLifecycleVersion",
        glide    : "com.github.bumptech.glide:compiler:$glideVersion",
]

//Dependencies of third parties
final thirdPartyDependencies = [
        gson         : "com.google.code.gson:gson:$gsonVersion",
        glide        : "com.github.bumptech.glide:glide:$glideVersion",
        spinKitLoader: "com.github.ybq:Android-SpinKit:$spinKitVersion",
]

//Dependencies of Google Play Services Libs
final playServicesDependencies = [
        location : "com.google.android.gms:play-services-location:17.0.0",
        maps     : "com.google.android.gms:play-services-maps:17.0.0",
]


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation networkDependencies.values()
    implementation supportDependencies.values()
    implementation jetpackDependencies.values()
    implementation thirdPartyDependencies.values()
    implementation playServicesDependencies.values()
    kapt kaptDependencies.values()


    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}